
buildscript {
    ext.kotlin_version = '1.7.22'
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'io.github.rodm', name: 'gradle-teamcity-plugin', version: '1.5'
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
    }
}

ext {
    version_prefix = '0.1'
    teamcity_version = '2020.1'
    // compile
    spring_version = '4.3.12.RELEASE'
    httpclient_version = '4.3.6' // version is relevant to one used inside TeamCity
    // tests
    jetty_version = '9.4.19.v20190610'
    testcontainers_version = '1.17.6'
    assertj_version = '1.7.1'
}

group = 'org.jetbrains.teamcity.sccache'
version = "${version_prefix}-SNAPSHOT"

if (System.getenv('BUILD_NUMBER') != null) {
    version = "${System.env.BUILD_NUMBER}"
}


subprojects {
    group = rootProject.group
    version = rootProject.version

    apply plugin: 'java'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'kotlin'

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions { jvmTarget = '1.8' }
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}".toString())
    }
    if (System.getenv('TEAMCITY_VERSION') != null) {
        test.ignoreFailures = true
    }

    test {
        useTestNG()
    }
}

wrapper {
    gradleVersion = '7.6.1'
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
}